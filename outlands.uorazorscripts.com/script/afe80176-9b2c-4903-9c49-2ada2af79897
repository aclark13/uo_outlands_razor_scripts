# This is an automated backup - check out https://outlands.uorazorscripts.com/script/afe80176-9b2c-4903-9c49-2ada2af79897 for latest
# Automation by Jaseowns
## Script: Training Magery to 100 using shelf
## Created by: Jaseowns#6694
#############################################

# Training Magery to 100 using shelf by Jaseowns
# UO Outlands
# https://youtu.be/zIGvPxBPTFk
# Requirements:
#   Have your loadout be set to
#   100 of BM, NS, SS, MR
# Invisibility: ~2100 Blood moss, Nightshade
# Summon a water elemental: >~ 5000 Blood moss, Spiders Silk, Mandrake Root#
if skill 'Magery' < 100
    if skill 'Magery' < 80
        if counttype 'Blood moss' backpack < 1 or counttype 'Nightshade' backpack < 1
            if findtype "storage shelf" ground any any 2 as the_shelf
                overhead "Resupply" 88
                menu the_shelf 1
                wait 500
            endif
        endif
        if mana > 30
            overhead "Invis" 88
            hotkey 'Invisibility'
            wft
            hotkey 'Target Self'
            wait 500
        endif 
        if mana < 30 and not findbuff "actively meditating"
            hotkey 'Meditation'
            wait 3500
        endif
    elseif skill 'Magery' < 100
        if counttype 'Blood moss' backpack < 1 or counttype "Spider's Silk" backpack < 1 or counttype "Mandrake Root%s%" backpack < 10
            if findtype "storage shelf" ground any any 2 as the_shelf
                overhead "Resupply" 88
                menu the_shelf 1
                wait 500
            endif
        endif
        if mana > 40
            if findtype "a water elemental" ground -1 -1 2
                say 'a water elemental release'
                wait 200
            endif
            hotkey "Water Elemental"
            wait 5000
        endif
        if mana < 40 and not findbuff "actively meditating"
            hotkey 'Meditation'
            wait 3500
        endif
    endif
    wait 1000
    loop
else
    overhead 'We did it!' 88
endif